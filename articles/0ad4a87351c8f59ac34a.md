---
title: "WinMergeãƒ—ãƒ©ã‚°ã‚¤ãƒ³ï¼ˆCOBOLè¡Œé ­ç•ªå·ç„¡è¦–ï¼‰ "
emoji: "ğŸ¤–"
type: "tech"  # tech: æŠ€è¡“è¨˜äº‹ / idea: ã‚¢ã‚¤ãƒ‡ã‚¢
topics: [ "cobol", "WinMerge" ]
published: true
created_at: "2016-08-06T17:42:43+09:00"
updated_at: "2019-05-03T14:25:24+09:00"
---
ä»•äº‹ã§COBOLã®ã‚½ãƒ¼ã‚¹ã‚³ãƒ¼ãƒ‰ã‚’æ¯”è¼ƒã—ãŸã„æ™‚ãŒã‚ã£ãŸã€‚
å…ˆè¼©æ–¹ã¯diffç­‰ã‚’ä½¿ã£ã¦ã„ãŸãŒã€å€‹äººçš„ã«WinMergeã‚’ä½¿ã„ãŸã‹ã£ãŸã®ã§ä½œæˆã€‚

COBOLã¯è¡Œé ­ã«6æ¡ã®è¡Œç•ªå·ã‚’æŒ¯ã‚‹ã“ã¨ãŒå¤šãã€WinMergeã§æ™®é€šã«å·®åˆ†ã‚’å–ã‚‹ã¨å¤‰æ›´è¡Œä»¥é™ã™ã¹ã¦ãŒå·®åˆ†ã«ãªã£ã¦ã—ã¾ã†ã€‚ã“ã®å•é¡Œã‚’è§£æ±ºã™ã‚‹ãŸã‚ã«ãƒ—ãƒ©ã‚°ã‚¤ãƒ³ã§è¡Œå…ˆé ­6æ¡ç›®ã¾ã§ã‚’é™¤å¤–ã™ã‚‹ã‚ˆã†ã«ã—ãŸã€‚


```vbnet:IgnoreCobolLineNo.sct
<scriptlet>
 
<implements type="Automation" id="dispatcher">
    <property name="PluginEvent">
        <get/>
    </property>
    <property name="PluginDescription">
        <get/>
    </property>
    <property name="PluginFileFilters">
        <get/>
    </property>
    <property name="PluginIsAutomatic">
        <get/>
    </property>
    <method name="UnpackFile"/>
    <method name="PackFile"/>
</implements>
 
<script language="VBS">
 
Option Explicit
 
Function get_PluginEvent()
    get_PluginEvent = "FILE_PACK_UNPACK"
End Function
 
Function get_PluginDescription()
    get_PluginDescription = "COBOLã‚½ãƒ¼ã‚¹ã‚³ãƒ¼ãƒ‰ã®è¡Œç•ªå·ã‚’å‰Šé™¤ã™ã‚‹(1-6æ¡ç›®)"
End Function
 
Function get_PluginFileFilters()
    get_PluginFileFilters = "\.cbl$;\.ecb$"
End Function
 
Function get_PluginIsAutomatic()
    get_PluginIsAutomatic = True
End Function
 
Function UnpackFile(fileSrc, fileDst, pbChanged, pSubcode)
    Dim fso
    Dim foSrc
    Dim foDst
     
    Set fso = CreateObject("Scripting.FileSystemObject")
     
    'èª­ã¿å–ã‚Šå°‚ç”¨ãƒ¢ãƒ¼ãƒ‰
    Set foSrc = fso.OpenTextFile(fileSrc, 1, True)
    'ä¸Šæ›¸ããƒ¢ãƒ¼ãƒ‰
    Set foDst = fso.OpenTextFile(fileDst, 2, True)
     
    '1è¡Œã¥ã¤èª­ã¿è¾¼ã‚€
    Dim srcLine
    Dim dstLine
    Do Until foSrc.AtEndOfStream
        srcLine = foSrc.ReadLine
        
        'è¡Œå…ˆé ­ã®è¡Œç•ªå·(6æ¡)ã‚’å‰Šé™¤ã™ã‚‹
        If Len(srcLine) - 6 <= 0 Then
            dstLine = ""
        Else
            dstLine = Right(srcLine, Len(srcLine) - 6)
        End If
        
        foDst.WriteLine(dstLine)
    Loop
     
    foSrc.Close
    foDst.Close
     
    Set foSrc = Nothing
    Set foDst = Nothing
    Set fso = Nothing
     
    pbChanged = True
    pSubcode = 0
    UnpackFile = True
End Function
 
Function PackFile(fileSrc, fileDst, pbChanged, pSubcode)
    PackFile = False
End Function
 
</script>
</scriptlet>
```
